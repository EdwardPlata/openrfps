#!/usr/bin/env coffee

program = require 'commander'
colors = require 'colors'
_ = require 'underscore'

program
  .option('-s, --skipsave', 'Save to .json relative to scraper file')
  .option('-l, --limit <n>', 'Stop after processing <n> results [10]', parseInt, 10)
  .option('-f, --force', "Force re-scraping (by default, we'll try to test against the cached .json files)")
  .parse(process.argv)

failingTests = 0

runAssertion = (msg, testFunc) ->
  if testFunc()
    console.log "#{msg}: ".yellow + "OK".green
  else
    console.log "#{msg}: ".yellow + "Not OK".red
    failingTests += 1

require('./utils/run_scraper') program, (parsedJson) ->

  return unless parsedJson

  runAssertion 'Scraper returns at least one result', ->
    (typeof parsedJson == 'object') && !_.isEmpty(parsedJson)

  runAssertion 'ID attribute is returned for all items', ->
    !_.isEmpty(parsedJson) && !_.find(parsedJson, ( (item) -> !item.id) )

  runAssertion 'ID attribute is unique for each item', ->
    pluckedIds = _.pluck(parsedJson, 'id')
    uniqIds = _.uniq(pluckedIds)
    !_.isEmpty(parsedJson) && _.isEqual(pluckedIds, uniqIds)

  runAssertion 'Title is returned for all items', ->
    !_.isEmpty(parsedJson) && !_.find(parsedJson, ( (item) -> !item.title) )

  if failingTests > 0
    process.exit(1)
  else
    process.exit(0)
